**AttackHit.jass** :  공격 판정 객체를 의미합니다. (공격력, 데미지타입, 데미지, 지상혹은공중, 영웅비영웅 등등 속성이 있음)

인터페이스를 통한 의사 상속으로 CircleAttackHit (원형 공격) , DonutAttackHit(중앙이 비어있는 도넛형 공격) 으로 상속됩니다.

이펙트에 붙어다닐 수 있으며 DoHit시간이 되면 공격을 재생하게 됩니다.

공격 재생시 이벤트를 발동합니다.

```lua
library AttackHit requires Extensions

interface AttackHit
    public player pAttackerPlayer
    public boolean hasCollider
    public real fColliderRadius
    public boolean canHitWhenAttackerDead
    //공격자
    public unit uAttacker
    ///데미지 이벤트
    public OnDamage onDamage
    ///아머타입
    public attacktype eAttackType
    ///데미지 타입
    public damagetype eDamageType
    ///데미지
    public real fDamage
    ///타이머
    public real fTimer
    ///시간
    public real fTime
    ///영웅 혹은 비영웅 대상인가?
    public HeroTargetType eHeroTargetType
    ///지상 혹은 공중 대상인가?
    public GroundType eGroundType
    ///건물 공격가능한가?
    public boolean CanTargetStructure
    ///이 공격을 재생합니다. (목표 시간까지 재생한 후 DoHit합니다.)
    public method Play takes real dt, Vector3 vPos returns boolean
    ///공격을 발현할 순간인가?
    public method CanDoHit takes Vector3 vPos returns boolean
    ///이 공격을 발현합니다.
    public method DoHit takes Vector3 vPos returns nothing
    //만료되었는가?
    public method isExpired takes nothing returns boolean
    
    public method AddOnDamage takes OnDamage onDamage returns nothing
   
    public method AddCollider takes integer fRadius returns nothing
    
    public method CanHitWhenAttackerDead takes nothing returns AttackHit
endinterface

function NewCircleAttackHit takes unit uAttacker, real fRadius, HeroTargetType eHeroTargetType, GroundType eGroundType, boolean CanTargetStructure , real fTime, real fDamage, attacktype eAttackType, damagetype eDamageType returns AttackHit
    return CircleAttackHit.create(uAttacker,fRadius,eHeroTargetType,eGroundType,CanTargetStructure,fTime,fDamage,eAttackType,eDamageType)
endfunction

struct CircleAttackHit extends AttackHit
    ///원의 반지름
    public real fRadius
    //'공격자'가 'x반지름' 원안의 모든 적대적대상에게 'x초'후 'x의 피해'를 a아머타입 b데미지타입으로 입힙니다.
    public static method create takes unit uAttacker, real fRadius, HeroTargetType eHeroTargetType, GroundType eGroundType, boolean CanTargetStructure , real fTime, real fDamage, attacktype eAttackType, damagetype eDamageType returns thistype
        local thistype this = thistype.allocate()
        set this.uAttacker = uAttacker
        set this.pAttackerPlayer = GetOwningPlayer(uAttacker)
        set this.fRadius = fRadius
        set this.eHeroTargetType = eHeroTargetType
        set this.eGroundType = eGroundType
        set this.CanTargetStructure = CanTargetStructure
        set this.fTimer = 0
        set this.fTime = fTime
        set this.fDamage = fDamage
        set this.eAttackType = eAttackType
        set this.eDamageType = eDamageType
        set this.hasCollider = false
        set this.fColliderRadius = 0
        set this.onDamage = NULL
        set this.canHitWhenAttackerDead = false
        return this
    endmethod
    ///returns booleans isDoHited
    public method Play takes real dt, Vector3 vPos returns boolean
        set fTimer = fTimer + dt
        call Log("HL1")
        if (CanDoHit(vPos)) then
            call Log("HL3")
            call DoHit(vPos)
            return true
        endif
        return false
    endmethod

    static thistype filter
    static method isTargetUnit takes nothing returns boolean
        if( not ( IsUnitEnemy(GetFilterUnit(), filter.pAttackerPlayer)) == true ) then
                call Log("HF1")
                return false
        endif
        if(not filter.CanTargetStructure) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_STRUCTURE)) then
                call Log("HF2")
                return false
            endif
        endif
        
        if(filter.eHeroTargetType == HeroTargetType.HeroOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_HERO)) then
                call Log("HF3")
                return false
            endif
        elseif(filter.eHeroTargetType == HeroTargetType.NonHeroOnly) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_HERO)) then
                call Log("HF4")
                return false
            endif
        endif
        
        if(filter.eGroundType == GroundType.GroundOnly) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_FLYING)) then
                call Log("HF5")
                return false
            endif
        elseif(filter.eGroundType == GroundType.FlyingOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_FLYING)) then
                call Log("HF6")
                return false
            endif
        endif
        
        if(filter.canHitWhenAttackerDead) then
            call Log("canHitWhenAttackerDead = true")
        else
            call Log("canHitWhenAttackerDead = false")
        endif
        if((not filter.canHitWhenAttackerDead) and isUnitDead(GetFilterUnit())) then
            call Log("HF7")
            return false
        endif
        return true
    endmethod

    public method CanDoHit takes Vector3 vPos returns boolean
        local boolean passedHitTest = false
        local UnitList unitList
        call Log("HL2-1")
        if(hasCollider) then
            set CircleAttackHit.filter = this
            set unitList = CreateUnitListInRange(vPos, fColliderRadius, Filter(function CircleAttackHit.isTargetUnit))
            set passedHitTest = not unitList.isEmpty()
            call unitList.Clear()
            call unitList.destroy()
            set CircleAttackHit.filter = NULL
        endif
        call Log("HL2-2")
        return (fTimer >= fTime) or passedHitTest
    endmethod

    private method DoHit takes Vector3 vPos returns nothing
        local unitNode iter = NULL
        local UnitList unitList = NULL
        call Log("HL4-1")
        set CircleAttackHit.filter = this
        set unitList = CreateUnitListInRange(vPos, fRadius, Filter(function CircleAttackHit.isTargetUnit))
        set iter = unitList.head
        loop
            exitwhen not (iter != NULL)
                call UnitDamageTarget(uAttacker,iter.data,fDamage,false,false,eAttackType,eDamageType,null)
                set iter = iter.next
        endloop
        if(onDamage != NULL and not unitList.isEmpty()) then
            call onDamage.execute(vPos, unitList)
        endif
        call unitList.destroy()
        set CircleAttackHit.filter = NULL
        call Log("HL4-2")
    endmethod

    public method isExpired takes nothing returns boolean
        return (not canHitWhenAttackerDead) and isUnitDead(uAttacker)
    endmethod

    private method onDestroy takes nothing returns nothing
        set uAttacker = null
        set pAttackerPlayer = null
        set onDamage = NULL
    endmethod
    
    public method AddOnDamage takes OnDamage onDamage returns nothing
        set this.onDamage = onDamage
    endmethod
    
    public method AddCollider takes integer fRadius returns nothing
        set hasCollider = true
        set fColliderRadius = fRadius
    endmethod
    
    public method CanHitWhenAttackerDead takes nothing returns AttackHit
        set this.canHitWhenAttackerDead = true
        return this
    endmethod
endstruct


function NewDonutAttackHit takes unit uAttacker, real fOutsideRadius, real fInsideRaduis, HeroTargetType eHeroTargetType, GroundType eGroundType, boolean CanTargetStructure , real fTime, real fDamage, attacktype eAttackType, damagetype eDamageType returns AttackHit
    return DonutAttackHit.create(uAttacker,fOutsideRadius,fInsideRaduis,eHeroTargetType,eGroundType,CanTargetStructure,fTime,fDamage,eAttackType,eDamageType)
endfunction

struct DonutAttackHit extends AttackHit
    ///원의 반지름
    public real fOutsideRadius
    public real fInsideRaduis
    //'공격자'가 'x반지름' 원거리 도넛안의 모든 적대적대상에게 'x초'후 'x의 피해'를 a아머타입 b데미지타입으로 입힙니다.
    public static method create takes unit uAttacker, real fOutsideRadius, real fInsideRaduis, HeroTargetType eHeroTargetType, GroundType eGroundType, boolean CanTargetStructure , real fTime, real fDamage, attacktype eAttackType, damagetype eDamageType returns thistype
        local thistype this = thistype.allocate()
        set this.uAttacker = uAttacker
        set this.pAttackerPlayer = GetOwningPlayer(uAttacker)
        set this.fOutsideRadius = fOutsideRadius
        set this.fInsideRaduis = fInsideRaduis
        set this.eHeroTargetType = eHeroTargetType
        set this.eGroundType = eGroundType
        set this.CanTargetStructure = CanTargetStructure
        set this.fTimer = 0
        set this.fTime = fTime
        set this.fDamage = fDamage
        set this.eAttackType = eAttackType
        set this.eDamageType = eDamageType
        set this.hasCollider = false
        set this.fColliderRadius = 0
        set this.onDamage = NULL
        set this.canHitWhenAttackerDead = false
        debug call Log("[Donut Create!]")
        return this
    endmethod
    ///returns booleans isDoHited
    public method Play takes real dt, Vector3 vPos returns boolean
        set fTimer = fTimer + dt
        if (CanDoHit(vPos)) then
            call DoHit(vPos)
            return true
        endif
        return false
    endmethod

    static thistype filter
    static method isTargetUnit takes nothing returns boolean
        if( not ( IsUnitEnemy(GetFilterUnit(), filter.pAttackerPlayer)) == true ) then
                return false
        endif
        if(not filter.CanTargetStructure) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_STRUCTURE)) then
                return false
            endif
        endif
        if(filter.eHeroTargetType == HeroTargetType.HeroOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_HERO)) then
                return false
            endif
        elseif(filter.eHeroTargetType == HeroTargetType.NonHeroOnly) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_HERO)) then
                return false
            endif
        endif

        if(filter.eGroundType == GroundType.GroundOnly) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_FLYING)) then
                call Log("HF5")
                return false
            endif
        elseif(filter.eGroundType == GroundType.FlyingOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_FLYING)) then
                call Log("HF6")
                return false
            endif
        endif
        
        if((not filter.canHitWhenAttackerDead) and isUnitDead(GetFilterUnit())) then
            call Log("HF7")
            return false
        endif
        return true
    endmethod

    public method CanDoHit takes Vector3 vPos returns boolean
        local boolean passedHitTest = false
        local UnitList unitList
        if(hasCollider) then
            set DonutAttackHit.filter = this
            set unitList = CreateUnitListInRange(vPos, fColliderRadius, Filter(function DonutAttackHit.isTargetUnit))
            set passedHitTest = not unitList.isEmpty()
            call unitList.Clear()
            call unitList.destroy()
            set DonutAttackHit.filter = NULL
        endif
        debug call Log("[Donut CanDoHit!]" + R2S(fTimer) + " >= "+ R2S(fTime))
        return (fTimer >= fTime) or passedHitTest
    endmethod

    private method DoHit takes Vector3 vPos returns nothing
        local unitNode iter = NULL
        local UnitList unitList = NULL
        debug call Log("Donut DoHit!")
        set DonutAttackHit.filter = this
        debug call Log("Donut DoHit2!")
        set unitList = CreateUnitListInDonutRange(vPos, fOutsideRadius, fInsideRaduis, Filter(function DonutAttackHit.isTargetUnit))
        debug call Log("Donut DoHit3!")
        set iter = unitList.head
        loop
            exitwhen not (iter != NULL)
                call UnitDamageTarget(uAttacker,iter.data,fDamage,false,false,eAttackType,eDamageType,null)
                set iter = iter.next
        endloop
        if(onDamage != NULL) then
            call onDamage.execute(vPos, unitList)
        endif
        call unitList.destroy()
        set DonutAttackHit.filter = NULL
    endmethod

    public method isExpired takes nothing returns boolean
        return (not canHitWhenAttackerDead) and isUnitDead(uAttacker)
    endmethod

    private method onDestroy takes nothing returns nothing
        set uAttacker = null
        set pAttackerPlayer = null
        set onDamage = NULL
    endmethod
    
    public method AddOnDamage takes OnDamage onDamage returns nothing
        set this.onDamage = onDamage
    endmethod
    
    public method AddCollider takes integer fRadius returns nothing
        set hasCollider = true
        set fColliderRadius = fRadius
    endmethod
    
    public method CanHitWhenAttackerDead takes nothing returns AttackHit
        set this.canHitWhenAttackerDead = true
        return this
    endmethod
endstruct

/*
struct CircleHealHit extends AttackHit
 ///원의 반지름
    public real fRadius
    //'공격자'가 'x반지름' 원안의 모든 적대적대상에게 'x초'후 'x의 피해'를 a아머타입 b데미지타입으로 입힙니다.
    public static method create takes unit uAttacker, real fRadius, HeroTargetType eHeroTargetType, GroundType eGroundType, boolean CanTargetStructure , real fTime, real fDamage, attacktype eAttackType, damagetype eDamageType returns thistype
        local thistype this = thistype.allocate()
        set this.uAttacker = uAttacker
        set this.pAttackerPlayer = GetOwningPlayer(uAttacker)
        set this.fRadius = fRadius
        set this.eHeroTargetType = eHeroTargetType
        set this.eGroundType = eGroundType
        set this.CanTargetStructure = CanTargetStructure
        set this.fTimer = 0
        set this.fTime = fTime
        set this.fDamage = fDamage
        set this.eAttackType = eAttackType
        set this.eDamageType = eDamageType
        set this.hasCollider = false
        set this.fColliderRadius = 0
        set this.onDamage = NULL
        return this
    endmethod
    ///returns booleans isDoHited
    public method Play takes real dt, Vector3 vPos returns boolean
        set fTimer = fTimer + dt
        if (CanDoHit(vPos)) then
            call DoHit(vPos)
            return true
        endif
        return false
    endmethod

    static thistype filter
    static method isTargetUnit takes nothing returns boolean
        if( not ( IsUnitEnemy(GetFilterUnit(), filter.pAttackerPlayer)) == true ) then
                return false
        endif
        if(not filter.CanTargetStructure) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_STRUCTURE)) then
                return false
            endif
        endif
        if(filter.eHeroTargetType == HeroTargetType.HeroOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_HERO)) then
                return false
            endif
        elseif(filter.eHeroTargetType == HeroTargetType.NonHeroOnly) then
            if (IsUnitType(GetFilterUnit(), UNIT_TYPE_HERO)) then
                return false
            endif
        endif

        if(filter.eGroundType == GroundType.GroundOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_GROUND)) then
                return false
            endif
        elseif(filter.eGroundType == GroundType.FlyingOnly) then
            if (not IsUnitType(GetFilterUnit(), UNIT_TYPE_FLYING)) then
                return false
            endif
        endif
        if(isUnitDead(GetFilterUnit())) then
            return false
        endif
        return true
    endmethod

    public method CanDoHit takes Vector3 vPos returns boolean
        local boolean passedHitTest = false
        local UnitList unitList
        if(hasCollider) then
            set CircleAttackHit.filter = this
            set unitList = CreateUnitListInRange(vPos, fColliderRadius, Filter(function CircleAttackHit.isTargetUnit))
            set passedHitTest = not unitList.isEmpty()
            call unitList.Clear()
            call unitList.destroy()
            set CircleAttackHit.filter = NULL
        endif
        return (fTimer >= fTime) or passedHitTest
    endmethod

    private method DoHit takes Vector3 vPos returns nothing
        local unitNode iter = NULL
        local UnitList unitList = NULL
        set CircleAttackHit.filter = this
        set unitList = CreateUnitListInRange(vPos, fRadius, Filter(function CircleAttackHit.isTargetUnit))
        set iter = unitList.head
        loop
            exitwhen not (iter != NULL)
                call UnitDamageTarget(uAttacker,iter.data,fDamage,false,false,eAttackType,eDamageType,null)
                set iter = iter.next
        endloop
        if(onDamage != NULL) then
            call onDamage.execute(vPos, unitList)
        endif
        call unitList.destroy()
        set CircleAttackHit.filter = NULL
    endmethod

    public method isExpired takes nothing returns boolean
        return isUnitDead(uAttacker)
    endmethod

    private method onDestroy takes nothing returns nothing
        set uAttacker = null
        set pAttackerPlayer = null
        set onDamage = NULL
    endmethod
    
    public method AddOnDamage takes OnDamage onDamage returns nothing
        set this.onDamage = onDamage
    endmethod
    
    public method AddCollider takes integer fRadius returns nothing
        set hasCollider = true
        set fColliderRadius = fRadius
    endmethod
endstruct
*/
endlibrary
```

**EffectManager.jass** : 이펙트 생성 & 업데이트를 관장 합니다.

이펙트 매니저는 특정 지점 혹은 유닛을 대상으로 특정 이펙트를 여러가지 옵션으로 재생할 수 있게해줍니다.

```lua
library EffectManager initializer Init requires EffectData

globals
    public EffectDataList effectDataList
    private timer updateTimer
    private constant real fUpdatePeriod = 0.04
endglobals

//just loop O(N) not O(N^2)
private function Update takes nothing returns nothing
    local EffectDataNode iter = effectDataList.head
    local EffectData data = NULL
    loop
        exitwhen not(iter != NULL)
            set data = iter.data
            ///지연된 Stopped 처리에 의해 필요합니다.
            if(data.isExpired()) then
                call data.destroy()
                set iter = effectDataList.RemoveNode(iter)
            else
                call data.Play(fUpdatePeriod)
                ///플레이후에 Timer에 의해 필요합니다.
                if(data.isExpired()) then
                    call data.destroy()
                    set iter = effectDataList.RemoveNode(iter)
                else
                    set iter = iter.next
                endif
            endif
    endloop
endfunction

private function Play takes EffectData effectData returns nothing
    call effectDataList.Add(effectData)
    //debug call Log("effectDataList.Count="+I2S(effectDataList.Count))
endfunction

function PlayEffectAtPos takes integer nEffectId, Vector3 vPos, real fTime, real fDelay returns EffectData
    local EffectData tempEffectData = PosEffectData.create(nEffectId,vPos,fTime,fDelay)
    //debug call Log("[PlayEffectAtLoc] Called!")
    call Play(tempEffectData)
    return tempEffectData
endfunction

function PlayEffectAtLocation takes integer nEffectId, location locPoint, real fTime, real fDelay returns EffectData
    local Vector3 newPos = Vector3.create(0,0,0).PosFromLocation(locPoint)
    return PlayEffectAtPos(nEffectId,newPos,fTime,fDelay)
endfunction


function PlayEffectAtUnit takes integer nEffectId, unit uTarget, real fTime, real fDelay returns EffectData
    local EffectData tempEffectData = UnitEffectData.create(nEffectId,uTarget,fTime,fDelay)
    call Play(tempEffectData)
    return tempEffectData
endfunction


private function Init takes nothing returns nothing
    set effectDataList = EffectDataList.create()
    set updateTimer = CreateTimer()
    call TimerStart(updateTimer,fUpdatePeriod,true,function Update)
endfunction

endlibrary
```

**EffectData.jass** : 이펙트 자체를 의미합니다. (이펙트 유닛을 포함하며, 이펙트 재생과 관련된 여러 속성을 포함합니다.)

또한 복수의 AttackHit을 가지고 있는 AttackHitList를 보유합니다.

AddAttackHit메서드를 통해 하나의 이펙트에 여러 공격판정 데이터를 추가할 수 있습니다.

이펙트 플레이 지연(delay), 회전, 대상 유닛을 따라갈지 아닐지 와 같은 속성 설정이 가능합니다.

EffectData 인터페이스를 UnitEffectData(유닛에 붙어다니는 이펙트데이터) , PosEffectData(지점에 존재하는 이펙트데이터)로 상속합니다.

```lua
library EffectData requires Extensions, LinkedList, Actions

globals

endglobals


interface EffectData
    //이펙트 고유번호
    public EffectId nEffectId
    //이펙트 유닛
    public unit uEffect = null
    //재생타이머
    public real fTimer
    //재생시간
    public real fTime
    ///지연된 재생 타이머
    public real fDelayedTimer
    ///지연된 재생시간
    public real fDelayedTime
    
    public AttackHitList attackHitList = NULL
    
    public boolean isStopped = false
    
    public real fRotateAngle = 0
    public real fSpeed = 1.0
    public boolean hasCustomAnimation = false
    public string strAnimationName = "stand"

    public Action_EffectData onEffectStart = NULL
    public Action_EffectData onEffectEnd = NULL
    
    public boolean hasDependency = false
    
    public method CreateEffectPos takes nothing returns Vector3
    //해당 이펙트를 재생합니다.
    public method Play takes real dt returns nothing
    //해당 이펙트의 재생을 영구적으로 중지하고 파괴합니다.
    public method Stop takes nothing returns nothing
    //해당 이펙트가 만료되었는지 확인합니다.
    public method isExpired takes nothing returns boolean
    //재생 이벤트
    public method OnStart takes nothing returns nothing
    //중지 이벤트
    public method OnEnd takes nothing returns nothing
    public method SetOnEffectEnd takes Action_EffectData onEffectEnd returns nothing
    ///회전
    public method RotateTo takes real fAngle returns EffectData
    public method RotateToUnit takes unit uTarget returns EffectData
    public method RotateToLoc takes location locTarget returns EffectData
    public method SetSpeed takes real fSpeed returns EffectData
    public method SetAnimation takes string strNaame returns EffectData
    //공격데이터 추가
    public method AddAttackHit takes AttackHit attackHit returns nothing
    //공격데이터 추가
    public method DontFollow takes nothing returns EffectData defaults NULL
endinterface

///해당 지점에 존재하는 이펙트 데이터
struct PosEffectData extends EffectData
    public Vector3 vTarget

    ///이펙트를 대상지점에 x초 동안 생성합니다.
    public static method create takes EffectId nEffectId, Vector3 vTarget, real fTime, real fDelay returns thistype
        local thistype this = thistype.allocate()
        set this.nEffectId = nEffectId
        //call RemoveUnit(CreateUnit(Player(0),nEffectId,vTarget.x,vTarget.y,bj_UNIT_FACING))
        set this.fTimer = 0
        set this.fTime = fTime
        set this.fDelayedTimer = 0
        set this.fDelayedTime = fDelay
        set this.vTarget = vTarget
        set this.isStopped = false
        if(fDelayedTime < 0.0001) then
            call this.StartPlay()
        endif
        return this
    endmethod
    
    private method StartPlay takes nothing returns nothing
        set uEffect = CreateUnit(playerNeutral,nEffectId,vTarget.x,vTarget.y,fRotateAngle)
        call SetUnitTimeScale(uEffect,fSpeed)
        if(hasCustomAnimation) then
            call SetUnitAnimation(uEffect,strAnimationName)
        endif
        call OnStart()
    endmethod
    
    ///이펙트를 대상 각도로 회전시킵니다.
    public method RotateTo takes real fAngle returns EffectData
        if(uEffect != null) then
            call SetUnitFacing(uEffect, fAngle)
        else
            set fRotateAngle = fAngle
        endif
        return this
    endmethod
    ///이펙트를 대상 유닛을 바라보게 회전시킵니다.
    public method RotateToUnit takes unit uTarget returns EffectData
        call RotateTo(AngleBetweenUnits(uEffect,uTarget))
        return this
    endmethod
    ///이펙트를 대상 지점을 바라보게 회전시킵니다.
    public method RotateToLoc takes location locTarget returns EffectData
        local location locEffect = GetUnitLoc(uEffect)
        call RotateTo(AngleBetweenPoints(locEffect,locTarget))
        call RemoveLocation(locEffect)
        set locEffect = null
        return this
    endmethod
    
    public method SetSpeed takes real fSpeed returns EffectData
        if(uEffect != null) then
            call SetUnitTimeScale(uEffect,fSpeed)
        else 
            set this.fSpeed = fSpeed
        endif
        return this
    endmethod

    public method SetAnimation takes string strName returns EffectData
        if(uEffect != null) then
            set hasCustomAnimation = true
            call SetUnitAnimation(uEffect,strName)
        else 
            set this.strAnimationName = strName
        endif
        return this
    endmethod


    private method isDelaying takes nothing returns boolean
        return (fDelayedTimer < fDelayedTime)
    endmethod

    public method Play takes real dt returns nothing
        local AttackHitNode iter = NULL
        local AttackHit data = NULL
        if(isDelaying()) then
            set fDelayedTimer = fDelayedTimer + dt
            if(not isDelaying()) then
                set fTimer = fTimer + (fDelayedTimer - fDelayedTime)
                call StartPlay()
            else
                //call Log("EP1")
                return
            endif
        else
            set fTimer = fTimer + dt
        endif
        
        if(attackHitList == NULL) then
            //call Log("EP2")
            return
        endif
        
        set iter = attackHitList.head
        loop
            exitwhen not(iter != NULL)
                set data = iter.data
                if(data.isExpired()) then
                    call data.destroy()
                    set iter = attackHitList.RemoveNode(iter)
                else
                    if(data.Play(dt, vTarget)) then
                        call data.destroy()
                        set iter = attackHitList.RemoveNode(iter)
                    else
                        set iter = iter.next
                    endif
                endif
        endloop
    endmethod

    public method Stop takes nothing returns nothing
        set isStopped = true
    endmethod

    public method isExpired takes nothing returns boolean
        //debug call Log("[isExpired] " +R2S(fTimer) + R2S(fTime))
        if (fTimer > fTime) then
            return true
        endif
        if(isStopped) then
            return true
        endif
        return false
    endmethod

    public method OnStart takes nothing returns nothing
        if(onEffectStart != NULL) then
            call onEffectStart.execute(this)
        endif
    endmethod

    private method OnEnd takes nothing returns nothing
        if(onEffectEnd != NULL) then
            call onEffectEnd.execute(this)
        endif
    endmethod
    
    public method CreateEffectPos takes nothing returns Vector3
        return vTarget.Copy()
    endmethod
    
    method onDestroy takes nothing returns nothing
        local AttackHitNode iter = NULL
        call OnEnd()
        
        if(attackHitList != NULL) then
            //call Log("ED1")
            set iter = attackHitList.head
            loop
                exitwhen not(iter != NULL)
                    call iter.data.destroy()
                    set iter = attackHitList.RemoveNode(iter)
            endloop
            call attackHitList.destroy()
        endif
        
        call RemoveUnit(uEffect)
        set uEffect = null
        call vTarget.destroy()
        if(onEffectStart != NULL) then
            set onEffectStart = NULL
        endif
        if(onEffectEnd != NULL) then
            set onEffectEnd = NULL
        endif
    endmethod
    
    public method AddAttackHit takes AttackHit attackHit returns nothing
        if(attackHitList == NULL) then
            set attackHitList = AttackHitList.create()
        endif
        call attackHitList.Add(attackHit)
    endmethod
    
    public method SetOnEffectEnd takes Action_EffectData onEffectEnd returns nothing
        set this.onEffectEnd = onEffectEnd
    endmethod
    
endstruct


//해당 유닛에 붙어다니는 이펙트 데이터
struct UnitEffectData extends EffectData
    private unit uRoot
    ///이펙트를 대상 유닛에 x초 동안 생성합니다.
    public static method create takes EffectId nEffectId, unit uRoot, real fTime, real fDelay returns thistype
        local thistype this = thistype.allocate()
        local location locTemp = GetUnitLoc(uRoot)
        set this.nEffectId = nEffectId
        set this.fTimer = 0
        set this.fTime = fTime
        set this.fDelayedTimer = 0
        set this.fDelayedTime = fDelay
        set this.uRoot = uRoot
        set this.isStopped = false
        set this.hasDependency = true
        if(fDelayedTime < 0.0001) then
            call this.StartPlay()
        endif
        call RemoveLocation(locTemp)
        set locTemp = null
        return this
    endmethod
    private method StartPlay takes nothing returns nothing
        set uEffect = CreateUnit(playerNeutral,nEffectId,GetUnitY(uRoot),GetUnitY(uRoot),fRotateAngle)
        call SetUnitTimeScale(uEffect,fSpeed)
        if(hasCustomAnimation) then
            call SetUnitAnimation(uEffect,strAnimationName)
        endif
        call UnitMoveToUnit(uEffect,uRoot)
        call OnStart()
    endmethod
    ///이펙트를 대상 각도로 회전시킵니다.
    public method RotateTo takes real fAngle returns EffectData
        call SetUnitFacing(uEffect, fAngle)
        return this
    endmethod
    ///이펙트를 대상 유닛을 바라보게 회전시킵니다.
    public method RotateToUnit takes unit uTarget returns EffectData
        call RotateTo(AngleBetweenUnits(uEffect,uTarget))
        return this
    endmethod
    ///이펙트를 대상 지점을 바라보게 회전시킵니다.
    public method RotateToLoc takes location locTarget returns EffectData
        local location locEffect = GetUnitLoc(uEffect)
        call RotateTo(AngleBetweenPoints(locEffect,locTarget))
        call RemoveLocation(locEffect)
        set locEffect = null
        return this
    endmethod

    public method SetSpeed takes real fSpeed returns EffectData
        if(uEffect != null) then
            call SetUnitTimeScale(uEffect,fSpeed)
        else 
            set this.fSpeed = fSpeed
        endif
        return this
    endmethod

    public method SetAnimation takes string strName returns EffectData
        if(uEffect != null) then
            set hasCustomAnimation = true
            call SetUnitAnimation(uEffect,strName)
        else 
            set this.strAnimationName = strName
        endif
        return this
    endmethod
    
    private method isDelaying takes nothing returns boolean
        return (fDelayedTimer < fDelayedTime)
    endmethod
    
    public method Play takes real dt returns nothing
        local AttackHitNode iter = NULL
        local AttackHit data = NULL
        local Vector3 pos = NULL
        if(isDelaying()) then
            set fDelayedTimer = fDelayedTimer + dt
            if(not isDelaying()) then
                set fTimer = fTimer + (fDelayedTimer - fDelayedTime)
                call StartPlay()
            else
                //call Log("2EP1")
                return
            endif
        else
            set fTimer = fTimer + dt
        endif
        if(hasDependency) then
            call UnitMoveToUnit(uEffect,uRoot)
        endif
        if(attackHitList == NULL) then
                //call Log("2EP2")
            return
        endif
        
        set iter = attackHitList.head
        set pos = Vector3.create(0,0,0).PosFromUnit(uRoot)
        loop
            exitwhen not(iter != NULL)
                set data = iter.data
                if(data.isExpired()) then
                    call data.destroy()
                    set iter = attackHitList.RemoveNode(iter)
                else
                    if(data.Play(dt, pos)) then
                        call data.destroy()
                        set iter = attackHitList.RemoveNode(iter)
                    else
                        set iter = iter.next
                    endif
                endif
        endloop
        call pos.destroy()
    endmethod

    public method Stop takes nothing returns nothing
        set isStopped = true
    endmethod

    public method isExpired takes nothing returns boolean
        if (fTimer > fTime) then
            return true
        endif
        if (hasDependency and isUnitDead(uRoot)) then
            return true
        endif
        if(isStopped) then
            return true
        endif
        return false
    endmethod

    public method OnStart takes nothing returns nothing
        if(onEffectStart != NULL) then
            call onEffectStart.execute(this)
        endif
    endmethod

    private method OnEnd takes nothing returns nothing
        debug call Log("[UnitEffectData-OnEnd]")
        if(onEffectEnd != NULL) then
            call onEffectEnd.execute(this)
        endif
    endmethod
    
    public method CreateEffectPos takes nothing returns Vector3
        return Vector3.create(0,0,0).PosFromUnit(uEffect)
    endmethod
    
    method onDestroy takes nothing returns nothing
        local AttackHitNode iter = NULL
        call OnEnd()
        
        if(attackHitList != NULL) then
            //call Log("2ED1")
            set iter = attackHitList.head
            loop
                exitwhen not(iter != NULL)
                    call iter.data.destroy()
                    set iter = attackHitList.RemoveNode(iter)
            endloop
            call attackHitList.destroy()
        endif
        
        if(onEffectStart != NULL) then
            set onEffectStart = NULL
        endif
        if(onEffectEnd != NULL) then
            set onEffectEnd = NULL
        endif
        
        call RemoveUnit(uEffect)
        set uEffect = null
        set uRoot= null
        
    endmethod
    
    public method SetOnEffectEnd takes Action_EffectData onEffectEnd returns nothing
        set this.onEffectEnd = onEffectEnd
    endmethod
    
    method AddAttackHit takes AttackHit attackHit returns nothing
        if(attackHitList == NULL) then
            set attackHitList = AttackHitList.create()
        endif
        call attackHitList.Add(attackHit)
        if(attackHit.fTime > this.fTime) then
            call LogWarning("Wrong AttackHit! attackHitTime must be longger then EffectTime!" + R2S(attackHit.fTime) +" > " + R2S(this.fTime))
        endif
    endmethod
    
    public method DontFollow takes nothing returns EffectData
        set hasDependency = false
        return this
    endmethod
endstruct

endlibrary
```

